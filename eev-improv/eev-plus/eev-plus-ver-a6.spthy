/*********************************************************************************

Estonian E-Voting Plus Protocol specification and verification for verifiability 
with the adversary model A6 that combines:

- Corrupted Network which is modeled by public channel for the communication 
between Voter and VC,

- Corrupted Voters who leak their EID to the adversary,  

- Corrupted VoteApp which receives a ciphertext from the adversary 
to generate a ballot, and

- Corrupted VC which leaks its secret key and the identifier when a ballot 
is received, and does not perform any check related to verification.

In this specification, we verify E2E Verifiability:

- Individual Verifiability (IV),
- Eligibility Verifiability 1 (ELI1), 
- Eligibility Verifiability 2 (ELI2), and 
- Result Integrity (RES).

Results:

- Success: RES: falsified, IV, ELI1, ELI2: verified
- Execution Time: 41:43.06 ~ 42m
- Attacks: Ballot stuffing by corrupted VoteApp
- Outcome: E2E verifiability is NOT satisfied.

--------------------------------------------------------------------

analyzed: eev-plus-verif-a6.spthy

  Individual_Verifiability (all-traces): falsified - found trace (47 steps)
  Eligibility_Verifiability_1 (all-traces): verified (3 steps)
  Eligibility_Verifiability_2 (all-traces): verified (3 steps)
  Result_Integrity (all-traces): falsified - found trace (16 steps)

**********************************************************************************/

theory EEV_Plus_Verifiability_A6
begin
 
builtins: asymmetric-encryption, signing, hashing

functions: enc/3, dec/2, zkp/4, ver/4

equations: dec(enc(m, pk(skE), r), skE) = m,
           ver(zkp(enc(m, pkE, r), m, r, pkid), enc(m, pkE, r), pkE, pkid) = true
              

// RA: Registration Authority
// EO: Election Organizer
// RS: Registration Service
// TMS: Time Marking Service
// VC: Vote Collector
// IBBP: I-Ballot Box Processor


// SETUP


rule RA_EID: //generates an EID for identities 
  let 
      pkid = pk(~skid) //public key of the identity
  in
    [ In(id), Fr(~skid) ] 
  --[ OnlyOnce(<id, 'EID'>) ]-> 
    [ !CertID(id, pkid), !EID(id, pkid, ~skid), Out(<id, pkid>) ]


rule EO_key: //generates an election key pair
  let
      pkE = pk(~skE) //public key of the election
  in
    [ Fr(~skE) ] 
  --[ BB_key(pkE), OnlyOnce('key') ]-> 
    [ !PkE(pkE), !SkE(~skE), Out(pkE) ]


rule E0_cand: //sets up candidates for the election
    [ In(v) ] --[  ]-> [ !Cand(v) ]


rule E0_voter: //determines voters eligible to vote
    [ In(id), !CertID(id, pkid) ] 
  --[ BB_reg(id), OnlyOnce(<id, 'voter'>) ]-> 
    [ !Voter(id) ]


rule TMS_cert: //gets certified for its public key
  let
      pkTMS = pk(~skTMS) //public key of TMS
  in
    [ Fr(~skTMS) ] 
  --[ OnlyOnce('TMScert') ]-> 
    [ !CertTMS(pkTMS), !SkTMS(~skTMS), Out(pkTMS) ]


rule RS_cert: //gets certified for its public key
  let
      pkRS = pk(~skRS) //public key of RS
  in
    [ Fr(~skRS) ] 
  --[ OnlyOnce('RScert') ]-> 
    [ !CertRS(pkRS), !SkRS(~skRS), Out(pkRS) ]


rule Corrupted_VC_cert: //gets certified for its public key,
                        //and leaks its secret key
  let
      pkVC = pk(~skVC) //public key of VC
  in
    [ Fr(~skVC) ] 
  --[ OnlyOnce('VCcert') ]-> 
    [ !CertVC(pkVC), !SkVC(~skVC), Out(<~skVC, pkVC>) ]


rule Corrupted_Voter_leak: //leaks EID to the adversary
    [ !Voter(id), !EID(id, pkid, skid) ] 
  --[ Corrupted(id) ]-> 
    [ Out(<id, pkid, skid>) ]


// VOTING


// In Estonian voting protocol, individual verification is allowed only 
// within a time frame, usually 30 minutes, with an identifier vid 
// provided by VC. The following rule starts and ends the public time 
// frame for verifying the votes by the voters.  

rule Start_ver: //starts time frame for individual verification
    [  ] --[ OnlyOnce(<$tOK, 'start'>) ]-> [ !StartTime($tOK) ]


rule End_ver: //ends time frame for individual verification
    [ !StartTime($tOK) ] 
  --[ EndTime($tOK), OnlyOnce(<$tOK, 'end'>) ]-> 
    [  ]


// The corrupted VoteApp generates a ballot after receiving a ciphertext 
// from the adversary and signing it with the EID card of the voter, and 
// generating the zkp for the knowledge of encryption randomness r. To 
// distinguish the last vote cast by the voter, we use fresh randomness 
// attached to the vote v. Then, the last generated randomness will 
// correspond to the last vote of the voter. We specify the communication 
// between VoteApp and VC with two rules in Tamarin. To remember the data 
// generated in Corrupted_VoteApp_vote, we record the fact Rec and consume 
// it in Corrupted_VoteApp_vid. Moreover, we record the fact !Rec2 that 
// will allow the verification of the vote corresponding to a different 
// ballot. This is needed to model the Pereira's attack. In the Pereira's 
// attack, the second generated ballot is verified by the first ballot's 
// confirmation. We also record the facts Ch# to model a private channel 
// between VoteApp and VC and another between VerApp and VC.

rule Corrupted_VoteApp_vote: //receives a ciphertext and a candidate
                             //from the adversary, signs the ciphertext,
                             //generates the zkp, and sends the ballot to VC
  let 
      s = sign(c, skid)       //signature for the ciphertext
      p = zkp(c, v1, r, pkid) //zkp for the knowledge of r
      b = <c, s, p>           //ballot structure
  in
    [ In(<v1, c, r>), !Voter(id), !EID(id, pkid, skid), !Cand(v), Fr(~u) ]
  --[ Vote(id, v), VoteB(id, b), Last(id, ~u) ]->
    [ Rec(id, v, b, r, ~u), !Rec2(id, v, b), Out(<id, b>) ]


rule Corrupted_VC_accept: //accepts the ballot, stores it with an identifier, 
                          //sends <pkid, h(c)> to TMS, and leaks the identifier
  let 
      b = <c, s, p> //ballot structure
  in
    [ In(<id, b>), !Voter(id), !CertID(id, pkid), !PkE(pkE), Fr(~vid) ]
  --[ Eq(verify(s, c, pkid), true), Eq(ver(p, c, pkE, pkid), true) ]->
    [ Vid(id, pkid, b, ~vid), Out(<pkid, h(c)>), Out(~vid) ]


rule TMS_tm: //signs the hash with the current time t
  let
      tm = sign(<pkid, hash, ~t>, skTMS) //TMS's signature 
  in
    [ In(<pkid, hash>), Fr(~t), !CertID(id, pkid), !SkTMS(skTMS) ]
  --[  ]->
    [ Out(<~t, tm>) ]


rule VC_req: //receives the time mark for the ciphertext hash, 
             //generates a registration request, and sends it to RS
  let
      b = <c, s, p> //ballot structure
      req = sign(<vid, h(b)>, skVC) //registration request
  in
    [ In(<t, tm>), Vid(id, pkid, b, vid), !CertTMS(pkTMS), !SkVC(skVC) ]
  --[ Eq(verify(tm, <pkid, h(c), t>, pkTMS), true) ]->
    [ Tm(id, pkid, b, vid, t, tm), Out(<vid, h(b), req>) ]


rule RS_reg: //registers <vid, h(b)> creating a registration confirmation
  let
      reg = sign(h(<vid, hash>), skRS) //registration confirmation
  in
    [ In(<vid, hash, req>), !CertVC(pkVC), !SkRS(skRS) ]
  --[ Eq(verify(req, <vid, hash>, pkVC), true) ]->
    [ !Registered(req, reg), Out(reg) ]


// VC stores the ballot after getting a time mark from TMS and registration 
// confirmation from RS, starts the verification time frame for the ballot 
// cast, and sends the ballot confirmation <vid, reg> to VoteApp. The fact 
// !Ver records the verification time frame tOK valid for that vid.

rule VC_store: //stores the ballot with registration confirmation 
    [ In(reg), Tm(id, pkid, b, vid, t, tm), !CertRS(pkRS), !StartTime(tOK) ]
  --[ Eq(verify(reg, h(<vid, h(b)>), pkRS), true), StoreB(id, b) ]->
    [ StoreData(id, pkid, b, vid, t, tm, reg), !Ver(vid, tOK), 
      Out(<vid, reg>) ]


// EO follows the databases of RS and VC. If a ballot is registered and
// stored, then it sends a notification to the voter who cast it via a 
// secure channel, saying a new ballot is stored.

rule EO_notify: //notifies the voter if a ballot is stored for them
    [ !Registered(req, reg), StoreData(id, pkid, b, vid, t, tm, reg) ]
  --[ Notification(id) ]->
    [ !Stored(id, pkid, b, vid, t, tm, reg)  ]


// VoteApp receives the ballot confirmation <vid, reg> from VC, verifies reg, 
// and generates a QR code to be used for the verification of the vote. The 
// corrupted VoteApp allows the verification of the vote recorded in Rec with 
// another ballot created by the voter. The fact !Voted records the vote the 
// voter chose and the QR code (vid, r).

rule Corrupted_VoteApp_vid: //verifies registration confirmation for the ballot cast
    [ In(<vid, reg>), Rec(id, v, b, r, u), !Rec2(id, v, b1), !CertRS(pkRS) ]
  --[ Eq(verify(reg, h(<vid, h(b1)>), pkRS), true) ]->
    [ !Voted(id, v, u, vid, r) ]
 

// The voter receives a ballot, and its registration confirmation via 
// VerApp if the verification time frame for that ballot has not expired, 
// and if the ballot corresponding to the vid is the last one stored 
// by VC for id. Then, VerApp verifies the validity of the ballot, 
// registration confirmation, and uses the randomness r inside the QR 
// code to generate the ciphertexts corresponding to all candidates. 
// The match between the encrypted ciphertext and the ballot ciphertext 
// shows the candidate the voter chose. If it is correct, the verification 
// is completed. In the following, we model the communication between 
// VerApp and VC in a single rule. In this rule, since VC is corrupt, 
// it does not perform any check related to the verification.

rule VerApp_ver: //verifies the vote cast with the QR code 
  let 
      b = <c, s, p>       //ballot structure
      c1 = enc(v, pkE, r) //encryption of the candidate v
  in
    [ !Voted(id, v, u, vid, r), !Stored(id, pkid, b, vid, t, tm, reg), 
      !Ver(vid, tOK), !PkE(pkE), !CertID(id, pkid), !CertRS(pkRS) ]
  --[ Eq(verify(reg, h(<vid, h(b)>), pkRS), true),
      Eq(verify(s, c, pkid), true), Eq(c, c1), 
      /*CheckTime(tOK), CheckB(id, b),*/ Verified(id, v, u) ]->
    [  ]

/*
restriction VerApp_ver1: //checks whether the time frame has ended
"
  All tOK #i. CheckTime(tOK) @i ==> not(Ex #j. EndTime(tOK) @j) 
" 

restriction VerApp_ver2: //checks whether the ballot to be verified is the last 
"
  All id b b1 #i #j #k. StoreB(id, b1) @i & StoreB(id, b) @j & CheckB(id, b) @k 
  & i < k & j < k ==> i < j | b = b1
"
*/

// TALLY


rule IBBP_verify: //verifies each ballot stored by VC, checks whether 
                  //it has registration confirmation
  let 
      b = <c, s, p> //ballot structure
  in
    [ !Voter(id), !Stored(id, pkid, b, vid, t, tm, reg), 
      !Registered(req, reg), !PkE(pkE), !CertID(id, pkid), 
      !CertVC(pkVC), !CertRS(pkRS) ]
  --[ Eq(verify(s, c, pkid), true), 
      Eq(ver(p, c, pkE, pkid), true),
      Eq(verify(req, <vid, h(b)>, pkVC), true), 
      Eq(verify(reg, h(<vid, h(b)>), pkRS), true) ]->
    [ !IBBPVerified(id, pkid, b, vid, t, tm, reg) ]


rule IBBP_tally: //selects the last ballot stored by VC for the tally
    [ !IBBPVerified(id, pkid, b, vid, t, tm, reg) ]
  --[ BB_tally(id, b), OnlyOnce(<id, 'tally'>) ]->
    [  ]


restriction IBBP_tally: //selects the last stored ballot
"
  All id b b1 #i #j #k. StoreB(id, b1) @i & StoreB(id, b) @j 
  & BB_tally(id, b) @k ==> i < j | b = b1
"


// RESTRICTIONS


restriction OnlyOnce:
" 
  All x #i #j. OnlyOnce(x) @i & OnlyOnce(x) @j ==> #i = #j
"
restriction Equality:
"
  All x y #i. Eq(x, y) @i ==> x = y
"
 

// LEMMAS 


lemma Individual_Verifiability:
"
  All id v u c s p pkE #i1 #i2 #i3. Verified(id, v, u) @i1 
  & BB_tally(id, <c, s, p>) @i2 & BB_key(pkE) @i3
  & ( All u1 #j1 #j2. Last(id, u1) @j1 & Last(id, u) @j2 ==> j1 < j2 | #j1 = #j2 )    
  ==> ( Ex r. c = enc(v, pkE, r) ) 
    | ( Ex #k. Notification(id) @k & i1 < k )
    | ( Ex #k. Corrupted(id) @k )
"

lemma Eligibility_Verifiability_1:
"
  All id v u #i. Verified(id, v, u) @i ==> Ex #j. BB_reg(id) @j 
"

lemma Eligibility_Verifiability_2:
"
  All id b #i. BB_tally(id, b) @i ==> Ex #j. BB_reg(id) @j 
" 

lemma Result_Integrity:
"
  All id c s p pkE #i #j. BB_tally(id, <c, s, p>) @i & BB_key(pkE) @j    
  ==> ( Ex v r #k. Vote(id, v) @k & c = enc(v, pkE, r) )
    | ( Ex #k. Corrupted(id) @k )
" 

lemma Result_Integrity_2:
"
  All id c s pkE #i #j. BB_tally(id, <c, s>) @i & BB_key(pkE) @j    
  ==> ( Ex v r #k. Vote(id, v) @k & c = enc(v, pkE, r) )
    | ( Ex #k. Corrupted(id) @k )
    | not( Ex v u #k. Verified(id, v, u) @k 
  & ( All u1 #j1 #j2. Last(id, u1) @j1 & Last(id, u) @j2 ==> j1 < j2 | #j1 = #j2 )
  & not( Ex #k1. Notification(id) @k1  & k < k1 ) ) 
" 
end